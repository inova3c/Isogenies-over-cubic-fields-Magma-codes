_<t>:=RationalFunctionField(Rationals());

//j-invariants for which group H_20 occurs

x20:=(32*(t+1)*(t^2-3)^3-4*(t^2-3)^4)/(t+1)^4;

//j-invariants for which group H_7 occurs 

x7:=(32*t-4)/t^4;

//j-invariants for which group H_3 occurs

x3:=-t^2+1728;

//j-invariants for which n-isogenies occur, for n=3,5,7,13;

f3:=(t+27)*(t+3)^3/t;

f5:=(t^2+10*t+5)^3/t;

f7:=(t^2+13*t+49)*(t^2+5*t+1)^3/t;

f13:=(t^2+5*t+13)*(t^4+7*t^3+20*t^2+19*t+1)^3/t;

R<x,y> := PolynomialRing(Rationals(), 2);


C37:=ProjectiveClosure(Curve(AffineSpace(R),Numerator(Evaluate(x7,x)-Evaluate(f3,y))));

Genus(C37);
//1

pts:=PointSearch(C37, 10);

E, mp := EllipticCurve(C37, pts[2]);
E;

Rank(E);
//0

G, mE:=TorsionSubgroup(E);
//G is isomorphic to Z/8Z

ptsE:={@@};

for i in [1..8] do
    ptsE := ptsE join {mE(i*G.1)};
end for;

ptsC37 := {@@};

for i in [1..8] do
    ptsC37 := ptsC37 join Points(ptsE[i]@@mp);
end for;

ptsC37;

//there are eight points on C37, three of them don't yield appropriate j-invariants
//the appropriate j-invariants are obtained by evaluating f3 in the coordinates of the obtained points

Evaluate(f3, -3);
Evaluate(f3, -81/4);
Evaluate(f3, -36);
Evaluate(f3, -243);
Evaluate(f3, -27);

//we obtain values 109503/64, -35937/4, -12288000, 0
// only the first two are non-CM
//for each of those, we test whether there is a 9-isogeny over the rationals
jlist:=[109503/64, -35937/4];

m9:=ClassicalModularPolynomial(9);


for j in jlist do
	Factorisation(Evaluate(m9, [x,j]));
end for;

//there is no 9-isogeny over Q for those j-invariants, so we can eliminate 27*4



C57:=ProjectiveClosure(Curve(AffineSpace(R),Numerator(Evaluate(x7,x)-Evaluate(f5,y))));

Genus(C57);
//1

pts:=PointSearch(C57, 10);

E, mp := EllipticCurve(C57, pts[2]);
E;

Rank(E);
//0

G, mE:=TorsionSubgroup(E);


ptsE:={@@};

for i in [1..4] do
    ptsE := ptsE join {mE(i*G.1)};
end for;

ptsC57 := {@@};

for i in [1..4] do
    ptsC57 := ptsC57 join Points(ptsE[i]@@mp);
end for;

ptsC57;

//there are 5 points, two of which give appropriate j-invariants

Evaluate(f5, -25/4);
Evaluate(f5, -20);

jlist:=[1026895/1024, -1723025/4];

m4:=ClassicalModularPolynomial(4);

for j in jlist do
	Factorisation(Evaluate(m4, [x,j]));
end for;

//the 4-isogenies are only defined over degree 6 number fields, so we can eliminate 5*4

C133:=ProjectiveClosure(Curve(AffineSpace(R),Numerator(Evaluate(x3,x)-Evaluate(f13,y))));

Genus(C133);
//1 

pts:=PointSearch(C133, 10);

E, mp:=EllipticCurve(C133, pts[1]);
E;

Rank(E);
//0 true

TorsionSubgroup(E);

// Z/(2), so rational points on E are (0,0) and the point at infinity

//taking their preimages under mp, turns out they both come from (1:0:0), so there are no nontrivial rational points on C133
//we have now eliminated 4*13


 

